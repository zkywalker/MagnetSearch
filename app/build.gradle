import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def cmd = 'git rev-list HEAD --count'

def gitVersion = cmd.execute().text.trim().toInteger()

def buildTime() {
    def df = new SimpleDateFormat("yyMMdd")
    df.setTimeZone(TimeZone.getTimeZone("UTC+08:00"))
    return df.format(new Date())
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.zky.tool.magnetsearch"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode gitVersion
        versionName "1.3.2.${buildTime()}${gitVersion}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias 'magnet'
            keyPassword 'magnetsearch123'
            storeFile file('/Users/zhangkun/AndroidStudioProjects/magnetsearch.jks')
            storePassword 'magnetsearch123'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            shrinkResources false

            proguardFile 'proguard-rules.pro'


            applicationVariants.all {
                variant ->
                    variant.outputs.all {
                        output ->
                            def outputFile = output.outputFile
                            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                                def fileName = "MagnetSearch_v${defaultConfig.versionName}.apk"
                                outputFileName = new File('apk', fileName)
                                //for windows
//                                output.outputFile = new File('C:/apk', fileName)
                            }

                    }
            }
        }

    }

    flavorDimensions "market"

    productFlavors {
        play {
            dimension "market"

            versionName "1.3.2.${buildTime()}${gitVersion}-play"
        }
        kuan{
            dimension "market"

            versionName "1.3.2.${buildTime()}${gitVersion}-kuan"
        }

    }
    packagingOptions {
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }
}

apply plugin: 'org.greenrobot.greendao'


greendao {
    //数据库schema版本号
    schemaVersion 1
    //dao的包名
    daoPackage 'org.zky.tool.magnetsearch.greendao.gen'
    //目标文件夹
    targetGenDir 'src/main/java'
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //noinspection GradleCompatible
    api 'androidx.appcompat:appcompat:1.0.2'
    api 'androidx.cardview:cardview:1.0.0'
    api 'com.google.android.material:material:1.0.0'
    api 'com.lzp.floatingactionbutton:floatingactionbuttonplus:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api 'com.google.code.gson:gson:2.8.5'
    api 'io.reactivex:rxjava:1.1.0'
    api 'io.reactivex:rxandroid:1.1.0'
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    api 'com.squareup.retrofit2:converter-gson:2.1.0'
    api 'org.greenrobot:greendao:3.2.2'
    api 'org.greenrobot:greendao-generator:3.2.2'
    testImplementation 'junit:junit:4.12'
    api 'com.google.zxing:core:3.3.0'
    api 'org.jsoup:jsoup:1.10.2'
    api 'com.google.firebase:firebase-core:9.6.1'
    api 'com.google.firebase:firebase-ads:9.6.1'
    api 'com.google.firebase:firebase-crash:9.6.1'
    api 'com.google.android.gms:play-services:9.6.1'
    api 'org.mod4j.org.apache.commons:lang:2.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}

